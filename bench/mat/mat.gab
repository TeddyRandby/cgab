require('std')

def Matrix { data n }

impl Matrix
  def new(n, data) { data n } end

  def + (other)
    self.n == other.n or panic()

    def N = self.n

    let data = List:new(N * N):slice

    for i in (N * N):each
      data[i] = self.data[i] + other.data[i]
    end

    Matrix:new(N, data)
  end

  def * (other)
    self.n == other.n or panic()

    def N = self.n

    let data = List:new(N * N):slice

    for i in N:each
      for j in N:each
        let sum = 0

        for k in N:each
          sum = sum + self.data[i*N+k] * other.data[k*N+j]
        end

        data[i*N+j] = sum
      end
    end

    Matrix:new(N, data)
  end

  def diag_sum
    let sum = 0

    def N = self.n

    for i in N:each
      sum = sum + self.data[i*(N + 1)]
    end

    sum
  end


end


let sum = 0

let N = 2

for _ in 5000:each
  let a = Matrix:new(N, List:new(N * N):map! do (_, i) i end :slice)
  let b = Matrix:new(N, List:new(N * N):map! do (_, i) i end :slice)

  let c = a + b
  let d = c * a * b

  sum = d:diag_sum
end

print(sum)
