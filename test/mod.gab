def describe(name, cb)
  print('\n--{name}--')
  cb()
end

def it(name, cb)
  print('{cb() and '✓' or '×'} it {name}')
end

def check(result, msg)
  result or print(msg)
end

def expect[](other)
  check(self == other, 'Expected {self} to be equal to {other}')
end

describe('Numbers', do ()
  it('should be equal to itself', do ()
    5:expect(5)
  end)
  it('should do math', do ()
    (5 * 2):expect(10) and
    (10 % 3):expect(1)
  end)
end)

describe('Strings', do ()
  it('should be equal to itself', do ()
    'hello':expect('hello')
  end)

  it('should concatenate', do ()
    ('hello'..' world'):expect('hello world')
  end)

  it('should interpolate', do ()
    ('{1} + {2} is {1 + 2}'):expect('1 + 2 is 3')
  end)
end)

describe('Closures', do ()
    let a = 1
    it('should capture', do ()
        a:expect(1)

        def test()
           a:expect(1)
           a  
        end

        test():expect(1)
        test():expect(1)
        test():expect(1)
    end)
    it('should capture by reference', do ()
        let b = 2

        def addOne()
            b = b + 1
        end

        b:expect(2)

        addOne()

        b:expect(3)

        addOne()
        addOne()

        b:expect(5)
    end)
end)

describe('Records', do ()
  it('should have properties', do ()
    let Point = {
      x: 1
      y: 2
    }

    expect(Point.x, 1) and
    expect(Point.y, 2)
  end)

  it('should be structurally shaped', do ()
    let pone = { x: 1 y: 2}
    let ptwo = { x: 3 y: 4}

    (pone?):expect(ptwo?)
  end)

  it('should set properties dynamically', do ()
    let obj = { [[]?]: 'test' }
    obj['y'] = 2

    (obj.y):expect(2) and
    (obj[[]?]):expect('test')
  end)

  it('should work for all kinds of keys', do ()
    let obj = {}
    obj[obj] = 2
    obj[{}?] = 3

    let i = do ()
      1
    end

    obj[i] = i 

    expect(obj[obj], 2) and
    expect(obj[{}?], 3) and
    expect(obj[i], i)
  end)

  it('should spread out the keys', do ()
    let obj = {
      test: 'hello'
      [{}?]: 'world'
    }

    let a, b = ..obj
    let c, d = ..obj?

    a:expect('hello') and
    b:expect('world') and
    c:expect('test') and
    d:expect({}?)
  end)

  it('should withstand this tricky mess', do ()
    let obj = {}

    def try_segfault()
      { try: 'segv' }
    end

    obj[try_segfault()] = 2

    expect(..obj, 2)
    expect(..((..obj?)?), 'try')
  end)
end)
